#define N_THREADS  5

static s_nVar1 := 0
static s_nVar2 := 0
static s_aCounters
static s_hMutex

procedure Main()

   local aThreads, i, lEnd, nSum

   ? Version()
   ? "Main start"
   s_aCounters := Array( N_THREADS )
   AFill( s_aCounters, 0 )
   aThreads := {}
   s_hMutex := zh_mutexCreate()
   lEnd := .F.
   ? "Starting threads:", ""
   for i := 1 to N_THREADS
      AAdd( aThreads, zh_threadStart( @thFunc(), i, @lEnd ) )
      ?? "<" + zh_ntos( i ) + ">"
   next
   ? "Wait 5 seconds or hit any key..."
   Inkey( 5 )
   lEnd := .T.
   ? "Waiting for threads..."
   AEval( aThreads, {| x | zh_threadJoin( x ) } )
   ? "Threads joined"
   nSum := 0
   AEval( s_aCounters, {| x | nSum += x } )
   ? "Sum of thread local counters:", nSum
   ? "Protected item result.......:", s_nVar2, ;
      iif( nSum == s_nVar2, "OK", "ERROR" )
   ? "Unprotected item result.....:", s_nVar1, "*"
   ? " * - can be different then local sum on real multi-CPU systems"
   ? "End of main"

   return

static procedure thFunc( nThread, lEnd )

   while ! lEnd
      s_nVar1++
      zh_mutexLock( s_hMutex )
      s_nVar2++
      zh_mutexUnlock( s_hMutex )
      s_aCounters[ nThread ]++
   enddo

   return