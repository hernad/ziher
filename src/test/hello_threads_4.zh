/*
 * Demonstration/test code for using mutexes to send/receive
 * messages between threads to synchronize divided jobs between
 * threads.
 *
 * Copyright 2008 Przemyslaw Czerpak <druzus / at / priv.onet.pl>
 *
 */

#define N_THREADS  5
#define N_JOBS     10000

static s_aCounters
static s_mtxJobs
static s_mtxResults

procedure Main()

   local aThreads, aResults, i, nDigit, nSum, nExpected

   ? Version()
   ? "Main start"

   s_aCounters := Array( N_THREADS )
   AFill( s_aCounters, 0 )
   aThreads := {}
   aResults := {}
   s_mtxJobs := zh_mutexCreate()
   s_mtxResults := zh_mutexCreate()

   ? "Starting threads:", ""
   for i := 1 to N_THREADS
      AAdd( aThreads, zh_threadStart( @thFunc() ) )
      ?? "<" + zh_ntos( i ) + ">"
   next

   ? "Sending jobs...", ""
   nDigit := 10
   for i := 1 to N_JOBS
      zh_mutexNotify( s_mtxJobs, nDigit )
      //?? "<" + zh_ntos( i ) + ">"
      nDigit++
   next

   ? "Sending terminate values..."
   for i := 1 to N_THREADS
      zh_mutexNotify( s_mtxJobs, NIL )
      ?? "<" + zh_ntos( i ) + ">"
   next

   ? "Collecting results...", ""
   for i := 1 to N_JOBS
      zh_mutexSubscribe( s_mtxResults,, @nDigit )
#if 0
      ?? "<" + zh_ntos( i ) + ">"
#endif
      AAdd( aResults, nDigit )
   next

   ? "Waiting for threads..."
   AEval( aThreads, {| x | zh_threadJoin( x ) } )
   ? "Threads joined"

   nSum := 0
   for each nDigit in aResults
      nSum += nDigit
   next

   nSum := Round( nSum, 2 )
   nExpected := Round( ( 10 + 10 + N_JOBS - 1 ) / 2 / 3 * N_JOBS, 2 )

   if Round( nSum - nExpected, 2 ) == 0
      ? "OK, final sum:", zh_ntos( nSum )
   else
      ? "ERROR, final sum:", zh_ntos( nSum ), ;
        "expected:", zh_ntos( nExpected )
   endif
   ? "End of main"

   return

static procedure thFunc()

   local xJob, xResult

   while .T.
      zh_mutexSubscribe( s_mtxJobs,, @xJob )
      if xJob == NIL
         exit
      endif
      xResult := xJob / 3
      zh_mutexNotify( s_mtxResults, xResult )
   enddo

   return