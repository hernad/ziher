/*
 * TIP simple logger class
 *
 * Copyright 2009 Viktor Szakats (vszakats.net/ziher)
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2, or (at your option)
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; see the file LICENSE.txt.  If not, write to
 * the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
 * Boston, MA 02110-1301 USA (or visit https://www.gnu.org/licenses/).
 *
 * As a special exception, the Ziher Project gives permission for
 * additional uses of the text contained in its release of Ziher.
 *
 * The exception is that, if you link the Ziher libraries with other
 * files to produce an executable, this does not by itself cause the
 * resulting executable to be covered by the GNU General Public License.
 * Your use of that executable is in no way restricted on account of
 * linking the Ziher library code into it.
 *
 * This exception does not however invalidate any other reasons why
 * the executable file might be covered by the GNU General Public License.
 *
 * This exception applies only to the code released by the Ziher
 * Project under the name Ziher.  If you copy code from other
 * Ziher Project or Free Software Foundation releases into a copy of
 * Ziher, as the General Public License permits, the exception does
 * not apply to the code that you add in this way.  To avoid misleading
 * anyone as to the status of such modified files, you must delete
 * this exception notice from them.
 *
 * If you write modifications of your own for Ziher, it is your choice
 * whether to permit this exception to apply to your modifications.
 * If you do not wish that, delete this exception notice.
 *
 */

#include "class.zhh"

#include "file_io.zhh"

CREATE CLASS TIPLog

   METHOD New( cFileName )
   METHOD Add( cMsg )
   METHOD Close()
   METHOD Clear()

   PROTECTED:

   VAR cFileName
   VAR hFile     INIT NIL

ENDCLASS

METHOD New( cFileName ) CLASS TIPLog

   zh_default( @cFileName, "hbtip" )

   IF Set( _SET_DEFEXTENSIONS )
      cFileName := zh_FNameExtSetDef( cFileName, ".log" )
   ENDIF

   ::cFileName := cFileName

   RETURN Self

METHOD Add( cMsg ) CLASS TIPLog

   LOCAL cDir, cName, cExt
   LOCAL n

   IF ::hFile == NIL

      zh_FNameSplit( ::cFileName, @cDir, @cName, @cExt )

      n := 1
      DO WHILE ( ::hFile := zh_vfOpen( zh_FNameMerge( cDir, cName + "-" + zh_ntos( n++ ), cExt ), FO_CREAT + FO_EXCL + FO_WRITE ) ) == NIL .AND. ;
         FError() != 3 /* path not found */
      ENDDO
   ENDIF

   IF ::hFile != NIL
      RETURN zh_vfWrite( ::hFile, cMsg ) == zh_BLen( cMsg )
   ENDIF

   RETURN .F.

METHOD Close() CLASS TIPLog

   LOCAL lRetVal

   IF ::hFile != NIL
      lRetVal := zh_vfClose( ::hFile )
      ::hFile := NIL
      RETURN lRetVal
   ENDIF

   RETURN .F.

METHOD Clear() CLASS TIPLog
   RETURN ::Close() .AND. zh_vfErase( ::cFileName ) != F_ERROR
