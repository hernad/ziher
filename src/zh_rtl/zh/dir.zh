/*
 * __Dir() function
 *
 * Copyright 1999 {list of individual authors and e-mail addresses}
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2, or (at your option)
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; see the file LICENSE.txt.  If not, write to
 * the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
 * Boston, MA 02110-1301 USA (or visit https://www.gnu.org/licenses/).
 *
 * As a special exception, the Ziher Project gives permission for
 * additional uses of the text contained in its release of Ziher.
 *
 * The exception is that, if you link the Ziher libraries with other
 * files to produce an executable, this does not by itself cause the
 * resulting executable to be covered by the GNU General Public License.
 * Your use of that executable is in no way restricted on account of
 * linking the Ziher library code into it.
 *
 * This exception does not however invalidate any other reasons why
 * the executable file might be covered by the GNU General Public License.
 *
 * This exception applies only to the code released by the Ziher
 * Project under the name Ziher.  If you copy code from other
 * Ziher Project or Free Software Foundation releases into a copy of
 * Ziher, as the General Public License permits, the exception does
 * not apply to the code that you add in this way.  To avoid misleading
 * anyone as to the status of such modified files, you must delete
 * this exception notice from them.
 *
 * If you write modifications of your own for Ziher, it is your choice
 * whether to permit this exception to apply to your modifications.
 * If you do not wish that, delete this exception notice.
 *
 */

#pragma -gc0

#include "directory.zhh"
#include "fileio.ch"

#define _DIR_HEADER  1

PROCEDURE __Dir( cFileMask )

   LOCAL cPath
   LOCAL cName
   LOCAL cExt

   IF Set( _SET_TRIMFILENAME )
      cFileMask := AllTrim( cFileMask )
   ENDIF

   IF cFileMask == ""

      QOut( __natMsg( _DIR_HEADER ) )

      AEval( zh_vfDirectory( zh_FNameMerge( Set( _SET_DEFAULT ), "*", ".dbf" ) ), ;
             {| aDirEntry | PutDbf( aDirEntry ) } )
   ELSE
      zh_FNameSplit( cFileMask, @cPath, @cName, @cExt )
      IF cPath == ""
         cPath := Set( _SET_DEFAULT )
      ENDIF

      AEval( zh_vfDirectory( zh_FNameMerge( cPath, cName, cExt ) ), ;
             {| aDirEntry | PutNormal( aDirEntry ) } )
   ENDIF

   QOut()

   RETURN

#define _DBF_HEAD_MARK  zh_BChar( 0x03 ) + zh_BChar( 0x06 ) + ;
                        zh_BChar( 0x30 ) + zh_BChar( 0x31 ) + ;
                        zh_BChar( 0x83 ) + zh_BChar( 0x86 ) + ;
                        zh_BChar( 0xE5 ) + zh_BChar( 0xE6 ) + ;
                        zh_BChar( 0xF5 ) + zh_BChar( 0xF6 )

STATIC PROCEDURE PutDBF( aDirEntry )

   LOCAL hFile
   LOCAL buffer
   LOCAL nRecCount := 0
   LOCAL dLastUpdate := zh_SToD()

   IF ( hFile := zh_vfOpen( aDirEntry[ F_NAME ], FO_READ ) ) != NIL

      buffer := zh_vfReadLen( hFile, 8 )

      IF zh_BLen( buffer ) == 8 .AND. zh_BAt( zh_BLeft( buffer, 1 ), _DBF_HEAD_MARK ) > 0
         nRecCount := Bin2L( zh_BSubStr( buffer, 5, 4 ) )
         dLastUpdate := zh_Date( zh_BPeek( buffer, 2 ) + 1900, ;
                                 zh_BPeek( buffer, 3 ), ;
                                 zh_BPeek( buffer, 4 ) )
      ENDIF

      zh_vfClose( hFile )
   ENDIF

   QOut( ;
      zh_UPadR( aDirEntry[ F_NAME ], 15 ) + ;
      Str( nRecCount, 12 ), "  ", ;
      DToC( dLastUpdate ) + ;
      Str( aDirEntry[ F_SIZE ], 12 ) )

   RETURN

STATIC PROCEDURE PutNormal( aDirEntry )

   LOCAL cName, cExt

   zh_FNameSplit( aDirEntry[ F_NAME ],, @cName, @cExt )

   /* Strict MS-DOS like formatting, it does not play well with long
      filenames which do not stick to 8.3 MS-DOS convention */

   QOut( ;
      zh_UPadR( cName, 8 ), ;
      zh_UPadR( zh_USubStr( cExt, 2 ), 3 ), ;
      Str( aDirEntry[ F_SIZE ], 8 ), "", ;
      zh_TToD( aDirEntry[ F_DATE ] ) )

   RETURN
