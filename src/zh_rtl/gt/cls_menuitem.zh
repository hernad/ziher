/*
 * MenuItem class
 *
 * Copyright 2000 Jose Lalin <dezac@corevia.com>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2, or (at your option)
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; see the file LICENSE.txt.  If not, write to
 * the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
 * Boston, MA 02110-1301 USA (or visit https://www.gnu.org/licenses/).
 *
 * As a special exception, the Ziher Project gives permission for
 * additional uses of the text contained in its release of Ziher.
 *
 * The exception is that, if you link the Ziher libraries with other
 * files to produce an executable, this does not by itself cause the
 * resulting executable to be covered by the GNU General Public License.
 * Your use of that executable is in no way restricted on account of
 * linking the Ziher library code into it.
 *
 * This exception does not however invalidate any other reasons why
 * the executable file might be covered by the GNU General Public License.
 *
 * This exception applies only to the code released by the Ziher
 * Project under the name Ziher.  If you copy code from other
 * Ziher Project or Free Software Foundation releases into a copy of
 * Ziher, as the General Public License permits, the exception does
 * not apply to the code that you add in this way.  To avoid misleading
 * anyone as to the status of such modified files, you must delete
 * this exception notice from them.
 *
 * If you write modifications of your own for Ziher, it is your choice
 * whether to permit this exception to apply to your modifications.
 * If you do not wish that, delete this exception notice.
 *
 */

#pragma -gc0

#include "class.zhh"

#include "button.zhh"

#define IS_IN( str, list )  ( "|" + ( str ) + "|" $ "|" + ( list ) + "|" )


CREATE CLASS MenuItem FUNCTION ZHMenuItem

   EXPORTED:

   VAR cargo

   METHOD caption( cCaption ) SETGET
   METHOD checked( lChecked ) SETGET
   METHOD data( boData ) SETGET
   METHOD enabled( lEnabled ) SETGET
   METHOD id( nID ) SETGET
   METHOD message( cMessage ) SETGET
   METHOD shortcut( nShortcut ) SETGET
   METHOD style( cStyle ) SETGET

   VAR __col      INIT -1  AS NUMERIC
   VAR __row      INIT -1  AS NUMERIC
   VAR __issep    INIT .F. AS LOGICAL

   METHOD isPopUp()

   METHOD New( cCaption, boData, nShortcut, cMessage, nID )

   PROTECTED:

   VAR cCaption   INIT ""
   VAR lChecked   INIT .F.
   VAR boData
   VAR lEnabled   INIT .T.
   VAR nID
   VAR cMessage
   VAR nShortcut
   VAR cStyle     INIT zh_UTF8ToStr( "√" ) + Chr( 16 ) /* LOW-ASCII "►" */

ENDCLASS

METHOD caption( cCaption ) CLASS MenuItem

   IF cCaption != NIL

      ::cCaption := checkVariableTypeAndValidBlock( Self, "CAPTION", cCaption, "C", 1001 )

      IF ::cCaption == ZH_MENU_SEPARATOR_UNI
         ::__issep  := .T.
         ::boData   := NIL
         ::lChecked := ::lEnabled := .F.
      ELSE
         ::__issep  := .F.
      ENDIF
   ENDIF

   RETURN ::cCaption

METHOD checked( lChecked ) CLASS MenuItem

   IF lChecked != NIL .AND. ! ::__issep
      ::lChecked := checkVariableTypeAndValidBlock( Self, "CHECKED", lChecked, "L", 1001 )
   ENDIF

   RETURN ::lChecked

METHOD data( boData ) CLASS MenuItem

   IF boData != NIL
      IF ZH_ISEVALITEM( boData )
         ::boData := boData
      ELSE
         ::boData := checkVariableTypeAndValidBlock( Self, "DATA", boData, "O", 1001, {|| IS_IN( boData:ClassName(), "POPUPMENU|ZH_POPUPMENU" ) } )
      ENDIF
   ENDIF

   RETURN ::boData

METHOD enabled( lEnabled ) CLASS MenuItem

   IF lEnabled != NIL .AND. ! ::__issep
      ::lEnabled := checkVariableTypeAndValidBlock( Self, "ENABLED", lEnabled, "L", 1001 )
   ENDIF

   RETURN ::lEnabled

METHOD id( nID ) CLASS MenuItem

   IF nID != NIL
      ::nID := checkVariableTypeAndValidBlock( Self, "ID", nID, "N", 1001 )
   ENDIF

   RETURN ::nID

METHOD message( cMessage ) CLASS MenuItem

   IF cMessage != NIL
      ::cMessage := checkVariableTypeAndValidBlock( Self, "MESSAGE", cMessage, "C", 1001 )
   ENDIF

   RETURN ::cMessage

METHOD shortcut( nShortcut ) CLASS MenuItem

   IF nShortcut != NIL
      ::nShortcut := checkVariableTypeAndValidBlock( Self, "SHORTCUT", nShortcut, "N", 1001 )
   ENDIF

   RETURN ::nShortcut

METHOD style( cStyle ) CLASS MenuItem

   IF cStyle != NIL
      ::cStyle := checkVariableTypeAndValidBlock( Self, "STYLE", cStyle, "C", 1001, {|| zh_ULen( cStyle ) == 2 } )
   ENDIF

   RETURN ::cStyle

METHOD isPopUp() CLASS MenuItem
   RETURN ZH_ISOBJECT( ::data ) .AND. IS_IN( ::data:ClassName(), "POPUPMENU|ZH_POPUPMENU" )

METHOD New( cCaption, boData, nShortcut, cMessage, nID ) CLASS MenuItem

   ::data      := boData
   ::nID       := zh_defaultValue( nID, 0 )
   ::cMessage  := zh_defaultValue( cMessage, "" )
   ::nShortcut := zh_defaultValue( nShortcut, 0 )
   ::caption   := cCaption

   RETURN Self

FUNCTION MenuItem( cCaption, boData, nShortcut, cMessage, nID )
   RETURN ZHMenuItem():New( cCaption, boData, nShortcut, cMessage, nID )


FUNCTION __MIColumn( o, nColumn )

   IF ZH_ISOBJECT( o ) .AND. o:ClassName() == "MENUITEM"

      IF ZH_ISNUMERIC( nColumn )
         o:__col := nColumn
      ENDIF

      RETURN o:__col
   ENDIF

   RETURN -1

FUNCTION __MIRow( o, nRow )

   IF ZH_ISOBJECT( o ) .AND. o:ClassName() == "MENUITEM"

      IF ZH_ISNUMERIC( nRow )
         o:__row := nRow
      ENDIF

      RETURN o:__row
   ENDIF

   RETURN -1
