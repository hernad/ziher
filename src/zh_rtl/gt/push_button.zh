/*
 * PushButton class
 *
 * Copyright 2000 Luiz Rafael Culik <culik@sl.conex.net>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2, or (at your option)
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; see the file LICENSE.txt.  If not, write to
 * the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
 * Boston, MA 02110-1301 USA (or visit https://www.gnu.org/licenses/).
 *
 * As a special exception, the Ziher Project gives permission for
 * additional uses of the text contained in its release of Ziher.
 *
 * The exception is that, if you link the Ziher libraries with other
 * files to produce an executable, this does not by itself cause the
 * resulting executable to be covered by the GNU General Public License.
 * Your use of that executable is in no way restricted on account of
 * linking the Ziher library code into it.
 *
 * This exception does not however invalidate any other reasons why
 * the executable file might be covered by the GNU General Public License.
 *
 * This exception applies only to the code released by the Ziher
 * Project under the name Ziher.  If you copy code from other
 * Ziher Project or Free Software Foundation releases into a copy of
 * Ziher, as the General Public License permits, the exception does
 * not apply to the code that you add in this way.  To avoid misleading
 * anyone as to the status of such modified files, you must delete
 * this exception notice from them.
 *
 * If you write modifications of your own for Ziher, it is your choice
 * whether to permit this exception to apply to your modifications.
 * If you do not wish that, delete this exception notice.
 *
 */

#pragma -gc0

#include "class.zhh"

#include "button.zhh"
#include "color.zhh"
#include "set_curs.zhh"

CREATE CLASS PushButton FUNCTION ZHPushButton

   EXPORTED:

   VAR cargo                          /* NOTE: CA-Cl*pper 5.3 has a bug, where this var cannot be assigned NIL. */

   VAR bmpXOff    INIT -1             /* NOTE: Fully compatible behaviour not implemented. */
   VAR bmpYOff    INIT -1             /* NOTE: Fully compatible behaviour not implemented. */
   VAR capXOff    INIT -1             /* NOTE: Fully compatible behaviour not implemented. */
   VAR capYOff    INIT -1             /* NOTE: Fully compatible behaviour not implemented. */
   VAR sizeX      INIT 0              /* NOTE: Fully compatible behaviour not implemented. */
   VAR sizeY      INIT 0              /* NOTE: Fully compatible behaviour not implemented. */

   METHOD display()
   METHOD hitTest( nMRow, nMCol )
   METHOD killFocus()
   METHOD select( nKey )
   METHOD setFocus()

   METHOD bitmap( cBitmap ) SETGET
   METHOD buffer() SETGET
   METHOD caption( cCaption ) SETGET
   METHOD col( nCol ) SETGET
   METHOD colorSpec( cColorSpec ) SETGET
   METHOD fBlock( bFBlock ) SETGET
   METHOD hasFocus() SETGET
   METHOD message( cMessage ) SETGET
   METHOD row( nRow ) SETGET
   METHOD sBlock( bSBlock ) SETGET
   METHOD typeOut() SETGET
   METHOD style( cStyle ) SETGET

   METHOD New( nRow, nCol, cCaption ) /* NOTE: This method is a Ziher extension [vszakats] */

   PROTECTED:

   VAR cBitmap    INIT ""
   VAR lBuffer    INIT .F.
   VAR cCaption
   VAR nCol
   VAR cColorSpec
   VAR bFBlock
   VAR lHasFocus  INIT .F.
   VAR cMessage   INIT ""
   VAR nRow
   VAR bSBlock
   VAR cStyle     INIT "<>"
   VAR lTypeOut   INIT .F.

ENDCLASS

METHOD setFocus() CLASS PushButton

   IF ! ::lHasFocus
      ::lHasFocus := .T.
      ::display()

      IF ZH_ISEVALITEM( ::bFBlock )
         Eval( ::bFBlock )
      ENDIF
   ENDIF

   RETURN Self

METHOD select( nKey ) CLASS PushButton

   LOCAL nKeyGot

   IF ::lHasFocus
      ::lbuffer := .T.
      ::display()

      IF ZH_ISNUMERIC( nKey )

         IF zh_keyChar( nKey ) == " "
            Inkey( 0.4 )
            DO WHILE zh_keyChar( nKey ) == " "
               nKey := Inkey( 0.1 )
            ENDDO
         ELSE
            DO WHILE nKey == zh_keyStd( nKeyGot := Inkey( 0 ) ) .OR. ;
                     nKey == nKeyGot
            ENDDO
         ENDIF
      ENDIF

      IF ZH_ISEVALITEM( ::bSBlock )
         Eval( ::bSBlock )
      ENDIF

      ::lBuffer := .F.
      ::display()
   ENDIF

   RETURN Self

METHOD killFocus() CLASS PushButton

   IF ::lHasFocus
      ::lHasFocus := .F.

      IF ZH_ISEVALITEM( ::bFBlock )
         Eval( ::bFBlock )
      ENDIF

      ::display()
   ENDIF

   RETURN Self

METHOD hitTest( nMRow, nMCol ) CLASS PushButton

   LOCAL nCurrentPos := 1
   LOCAL nLen := zh_ULen( ::cCaption )
   LOCAL nStyleLen
   LOCAL nAccelPos

   IF ( nAccelPos := zh_UAt( "&", ::cCaption ) ) > 0 .AND. nAccelPos < nLen
      nLen--
   ENDIF

   IF ( nStyleLen := zh_ULen( ::cStyle ) ) == 2
      nLen += 2
   ELSEIF nStyleLen == 8
      nCurrentPos := 3
      nLen += 2
   ENDIF

   IF nMRow >= ::Row .AND. ;
      nMCol >= ::Col .AND. ;
      nMRow < ::Row + nCurrentPos .AND. ;
      nMCol < ::Col + nLen
      RETURN HTCLIENT
   ENDIF

   RETURN HTNOWHERE

METHOD display() CLASS PushButton

   LOCAL cColor
   LOCAL cStyle := ::cStyle
   LOCAL cCaption := ::cCaption
   LOCAL nRow := ::nRow
   LOCAL nCol := ::nCol
   LOCAL nPos

   DispBegin()

   DO CASE
   CASE ::lBuffer
      cColor := zh_ColorIndex( ::cColorSpec, 2 )
   CASE ::lHasFocus
      cColor := zh_ColorIndex( ::cColorSpec, 1 )
   OTHERWISE
      cColor := zh_ColorIndex( ::cColorSpec, 0 )
   ENDCASE

   IF ( nPos := zh_UAt( "&", cCaption ) ) == 0
   ELSEIF nPos == zh_ULen( cCaption )
      nPos := 0
   ELSE
      cCaption := zh_UStuff( cCaption, nPos, 1, "" )
   ENDIF

   IF ! Empty( cStyle )

      nCol++

      IF zh_ULen( cStyle ) == 2
         zh_DispOutAt( ::nRow, ::nCol, zh_USubStr( cStyle, 1, 1 ), cColor )
         zh_DispOutAt( ::nRow, ::nCol + zh_ULen( cCaption ) + 1, zh_USubStr( cStyle, 2, 1 ), cColor )
      ELSE
         nRow++
         zh_DispBox( ::nRow, ::nCol, ::nRow + 2, ::nCol + zh_ULen( cCaption ) + 1, cStyle, cColor )
      ENDIF
   ENDIF

   IF ! Empty( cCaption )

      zh_DispOutAt( nRow, nCol, cCaption, cColor )

      IF nPos != 0
         zh_DispOutAt( nRow, nCol + nPos - 1, zh_USubStr( cCaption, nPos, 1 ), zh_ColorIndex( ::cColorSpec, 3 ) )
      ENDIF
   ENDIF

   DispEnd()

   RETURN Self

METHOD bitmap( cBitmap ) CLASS PushButton

   IF cBitmap != NIL
      ::cBitmap := checkVariableTypeAndValidBlock( Self, "BITMAP", cBitmap, "C", 1001 )
   ENDIF

   RETURN ::cBitmap

METHOD buffer() CLASS PushButton
   RETURN ::lBuffer

METHOD caption( cCaption ) CLASS PushButton

   IF cCaption != NIL
      ::cCaption := checkVariableTypeAndValidBlock( Self, "CAPTION", cCaption, "C", 1001 )
   ENDIF

   RETURN ::cCaption

METHOD col( nCol ) CLASS PushButton

   IF nCol != NIL
      ::nCol := checkVariableTypeAndValidBlock( Self, "COL", nCol, "N", 1001 )
   ENDIF

   RETURN ::nCol

METHOD colorSpec( cColorSpec ) CLASS PushButton

   IF cColorSpec != NIL
      ::cColorSpec := checkVariableTypeAndValidBlock( Self, "COLORSPEC", cColorSpec, "C", 1001, ;
         {|| ! Empty( zh_ColorIndex( cColorSpec, 3 ) ) .AND. Empty( zh_ColorIndex( cColorSpec, 5 ) ) } )
   ENDIF

   RETURN ::cColorSpec

METHOD fBlock( bFBlock ) CLASS PushButton

   IF PCount() > 0
      ::bFBlock := iif( bFBlock == NIL, NIL, checkVariableTypeAndValidBlock( Self, "FBLOCK", bFBlock, "B", 1001 ) )
   ENDIF

   RETURN ::bFBlock

METHOD hasFocus() CLASS PushButton
   RETURN ::lHasFocus

METHOD message( cMessage ) CLASS PushButton

   IF cMessage != NIL
      ::cMessage := checkVariableTypeAndValidBlock( Self, "MESSAGE", cMessage, "C", 1001 )
   ENDIF

   RETURN ::cMessage

METHOD row( nRow ) CLASS PushButton

   IF nRow != NIL
      ::nRow := checkVariableTypeAndValidBlock( Self, "ROW", nRow, "N", 1001 )
   ENDIF

   RETURN ::nRow

METHOD sBlock( bSBlock ) CLASS PushButton

   IF PCount() > 0
      ::bSBlock := iif( bSBlock == NIL, NIL, checkVariableTypeAndValidBlock( Self, "SBLOCK", bSBlock, "B", 1001 ) )
   ENDIF

   RETURN ::bSBlock

METHOD typeOut() CLASS PushButton
   RETURN .F.

METHOD style( cStyle ) CLASS PushButton

   IF cStyle != NIL
      ::cStyle := checkVariableTypeAndValidBlock( Self, "STYLE", cStyle, "C", 1001, {|| cStyle == "" .OR. zh_ULen( cStyle ) == 2 .OR. zh_ULen( cStyle ) == 8 } )
   ENDIF

   RETURN ::cStyle

METHOD New( nRow, nCol, cCaption ) CLASS PushButton

   LOCAL cColor

   IF ! ZH_ISNUMERIC( nRow ) .OR. ;
      ! ZH_ISNUMERIC( nCol )
      RETURN NIL
   ENDIF

   __defaultNIL( @cCaption, "" )

   ::caption  := cCaption
   ::nCol     := nCol
   ::nRow     := nRow

   IF IsDefColor()
      ::cColorSpec := "W/N,N/W,W+/N,W+/N"
   ELSE
      cColor := SetColor()
      ::cColorSpec := ;
         zh_ColorIndex( cColor, CLR_UNSELECTED ) + "," + ;
         zh_ColorIndex( cColor, CLR_ENHANCED   ) + "," + ;
         zh_ColorIndex( cColor, CLR_STANDARD   ) + "," + ;
         zh_ColorIndex( cColor, CLR_BACKGROUND )
   ENDIF

   RETURN Self

FUNCTION PushButton( nRow, nCol, cCaption )
   RETURN ZHPushButton():New( nRow, nCol, cCaption )

FUNCTION _PushButt_( cCaption, cMessage, cColorSpec, bFBlock, bSBlock, cStyle, nSizeX, nSizeY, nCapXOff, nCapYOff, cBitmap, nBmpXOff, nBmpYOff )

   LOCAL o := ZHPushButton():New( Row(), Col(), cCaption )

   o:message   := cMessage
   o:colorSpec := cColorSpec
   o:fBlock    := bFBlock
   o:sBlock    := bSBlock
   o:style     := cStyle
   o:sizeX     := nSizeX
   o:sizeY     := nSizeY
   o:capXOff   := nCapXOff
   o:capYOff   := nCapYOff
   o:bitmap    := cBitmap
   o:bmpXOff   := nBmpXOff
   o:bmpYOff   := nBmpYOff

   RETURN o
