/*
 * Class ZHPersistent
 *
 * Copyright 2001 Antonio Linares <alinares@fivetech.com>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2, or (at your option)
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; see the file LICENSE.txt.  If not, write to
 * the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
 * Boston, MA 02110-1301 USA (or visit https://www.gnu.org/licenses/).
 *
 * As a special exception, the Ziher Project gives permission for
 * additional uses of the text contained in its release of Ziher.
 *
 * The exception is that, if you link the Ziher libraries with other
 * files to produce an executable, this does not by itself cause the
 * resulting executable to be covered by the GNU General Public License.
 * Your use of that executable is in no way restricted on account of
 * linking the Ziher library code into it.
 *
 * This exception does not however invalidate any other reasons why
 * the executable file might be covered by the GNU General Public License.
 *
 * This exception applies only to the code released by the Ziher
 * Project under the name Ziher.  If you copy code from other
 * Ziher Project or Free Software Foundation releases into a copy of
 * Ziher, as the General Public License permits, the exception does
 * not apply to the code that you add in this way.  To avoid misleading
 * anyone as to the status of such modified files, you must delete
 * this exception notice from them.
 *
 * If you write modifications of your own for Ziher, it is your choice
 * whether to permit this exception to apply to your modifications.
 * If you do not wish that, delete this exception notice.
 *
 */

#pragma -gc0

#include "class.zhh"

REQUEST Array

#define _INDENT  3

CREATE CLASS ZHPersistent

   METHOD CreateNew() INLINE Self
   METHOD LoadFromFile( cFileName, lIgnoreErrors ) INLINE ::LoadFromText( zh_MemoRead( cFileName ), lIgnoreErrors )
   METHOD LoadFromText( cObjectText, lIgnoreErrors )
   METHOD SaveToText( cObjectName, nIndent )
   METHOD SaveToFile( cFileName ) INLINE zh_MemoWrit( cFileName, ::SaveToText() )

ENDCLASS

METHOD LoadFromText( cObjectText, lIgnoreErrors ) CLASS ZHPersistent

   LOCAL nPos
   LOCAL cLine
   LOCAL cProp
   LOCAL cInd
   LOCAL uValue
   LOCAL aWords
   LOCAL lStart := .T.
   LOCAL aObjects := { Self }
   LOCAL bError

   IF cObjectText == ""
      RETURN .F.
   ENDIF

   bError := iif( zh_defaultValue( lIgnoreErrors, .F. ), __BreakBlock(), ErrorBlock() )

   FOR EACH cLine IN zh_ATokens( cObjectText, .T. )

      cLine := AllTrim( cLine )

      BEGIN SEQUENCE WITH bError

         cProp := NIL

         IF Empty( cLine ) .OR. zh_LeftEq( cLine, "//" )
            /* ignore comments and empty lines */
         ELSEIF zh_LeftEq( cLine, "::" )

            IF ( nPos := At( ":=", cLine ) ) > 0
               cProp := RTrim( SubStr( cLine, 3, nPos - 3 ) )
               uValue := &( LTrim( SubStr( cLine, nPos + 2 ) ) )
            ELSEIF ( nPos := At( "=", cLine ) ) > 0 /* fix for older versions */
               cProp := RTrim( SubStr( cLine, 3, nPos - 3 ) )
               uValue := &( LTrim( SubStr( cLine, nPos + 1 ) ) )
            ENDIF
         ELSE
            aWords := zh_ATokens( zh_asciiUpper( cLine ) )
            SWITCH aWords[ 1 ]
            CASE "OBJECT"
               IF lStart
                  lStart := .F.
               ELSEIF aWords[ Len( aWords ) - 1 ] == "AS" .AND. zh_LeftEq( aWords[ 2 ], "::" )
                  cProp := SubStr( AllTrim( SubStr( cLine, 7, RAt( " AS ", cLine ) - 7 ) ), 3 )
                  uValue := &( aTail( aWords ) )():CreateNew()
               ENDIF
               EXIT
            CASE "ENDOBJECT"
               ASize( aObjects, Len( aObjects ) - 1 )
               EXIT
            CASE "ARRAY"
               IF zh_LeftEq( aWords[ 2 ], "::" )
                  cProp := SubStr( AllTrim( SubStr( cLine, 6, RAt( " LEN ", cLine ) - 6 ) ), 3 )
                  uValue := Array( Val( ATail( aWords ) ) )
               ENDIF
               EXIT
            ENDSWITCH
         ENDIF

         IF ! Empty( cProp )
            IF ( nPos := At( "[", cProp ) ) > 0
               cInd := zh_StrReplace( SubStr( cProp, nPos + 1, Len( cProp ) - nPos - 1 ), ;
                                      { " " => "", "][" => "," } )
               cProp := Left( cProp, nPos - 1 )
               ATail( aObjects ):&cProp[ &cInd ] := uValue
            ELSE
               ATail( aObjects ):&cProp := uValue
            ENDIF
         ENDIF

      END SEQUENCE

      IF Empty( aObjects )
         EXIT
      ENDIF
   NEXT

   RETURN .T.

METHOD SaveToText( cObjectName, nIndent ) CLASS ZHPersistent

   LOCAL oNew := &( ::ClassName() + "()" ):CreateNew()
   LOCAL cProp
   LOCAL uValue
   LOCAL uNewValue
   LOCAL cObject
   LOCAL cType
   LOCAL lSpacer := .T.

   LOCAL cEOL := Set( _SET_EOL )

   IF ! ZH_ISSTRING( cObjectName )
      cObjectName := "o" + ::ClassName()
   ENDIF

   IF ZH_ISNUMERIC( nIndent )
      nIndent += _INDENT
   ELSE
      nIndent := 0
   ENDIF

   cObject := iif( nIndent > 0, cEOL, "" ) + Space( nIndent ) + ;
      "OBJECT " + iif( nIndent != 0, "::", "" ) + cObjectName + " AS " + ;
      ::ClassName() + cEOL

   FOR EACH cProp IN __clsGetProperties( ::ClassH )

      uValue := Self:&cProp
      uNewValue := oNew:&cProp

      IF ! ( cType := ValType( uValue ) ) == ValType( uNewValue ) .OR. ;
         ! uValue == uNewValue

         SWITCH cType
         CASE "A"
            cObject += ArrayToText( uValue, cProp, nIndent + _INDENT )
            lSpacer := .T.
            EXIT

         CASE "O"
            IF __objDerivedFrom( uValue, "ZHPersistent" )
               cObject += uValue:SaveToText( cProp, nIndent )
               lSpacer := .T.
            ENDIF
            EXIT

         CASE "B"
         CASE "P"
            /* ignore codeblock and pointer items */
            EXIT

         OTHERWISE
            IF lSpacer
               lSpacer := .F.
               cObject += cEOL
            ENDIF
            cObject += Space( nIndent + _INDENT ) + "::" + ;
               cProp + " := " + zh_ValToExp( uValue ) + ;
               cEOL
         ENDSWITCH
      ENDIF
   NEXT

   RETURN cObject + cEOL + Space( nIndent ) + "ENDOBJECT" + cEOL

STATIC FUNCTION ArrayToText( aArray, cName, nIndent )

   LOCAL cEOL := Set( _SET_EOL )

   LOCAL cArray := cEOL + Space( nIndent ) + "ARRAY ::" + cName + ;
      " LEN " + zh_ntos( Len( aArray ) ) + cEOL
   LOCAL uValue

   FOR EACH uValue IN aArray

      SWITCH ValType( uValue )
      CASE "A"
         cArray += ArrayToText( uValue, cName + ;
            "[ " + zh_ntos( uValue:__enumIndex() ) + " ]", ;
            nIndent + _INDENT ) + cEOL
         EXIT

      CASE "O"
         IF __objDerivedFrom( uValue, "ZHPersistent" )
            cArray += uValue:SaveToText( cName + ;
               "[ " + zh_ntos( uValue:__enumIndex() ) + " ]", nIndent )
         ENDIF
         EXIT

      CASE "B"
      CASE "P"
         /* ignore codeblock and pointer items */
         EXIT

      OTHERWISE
         IF uValue:__enumIsFirst()
            cArray += cEOL
         ENDIF
         cArray += Space( nIndent + _INDENT ) + "::" + cName + ;
            "[ " + zh_ntos( uValue:__enumIndex() ) + " ]" + " := " + ;
            zh_ValToExp( uValue ) + cEOL
      ENDSWITCH
   NEXT

   RETURN cArray + cEOL + Space( nIndent ) + "ENDARRAY" + cEOL
