/*
 * CT3 file functions:
 *    FileCopy(), FileCOpen(), FileCCLose(), FileAppend()
 *
 *    Author...: Frederic J. Bell
 *    Dated....: 1994-06-17
 *    Revised..: 1994-09-20
 *    Purpose..: Replaces the following CA-T*ols functions which generate GPF's
 *               FileCopy(), FileCOpen(), FileAppend()!
 *    Relies on: Clipper (can you believe it!)
 *    Compile..: /n /m /w /[/p /b /l] /es2
 *    Notes....:
 *    No copyright - released into the public domain NSA.
 *
 * Copyright 2007 Przemyslaw Czerpak
 *
 *    added FileCDaTi() and rewritten above functions for CT3 compatibility
 *    and some problems fixing,
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2, or (at your option)
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; see the file LICENSE.txt.  If not, write to
 * the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
 * Boston, MA 02110-1301 USA (or visit https://www.gnu.org/licenses/).
 *
 * As a special exception, the Ziher Project gives permission for
 * additional uses of the text contained in its release of Ziher.
 *
 * The exception is that, if you link the Ziher libraries with other
 * files to produce an executable, this does not by itself cause the
 * resulting executable to be covered by the GNU General Public License.
 * Your use of that executable is in no way restricted on account of
 * linking the Ziher library code into it.
 *
 * This exception does not however invalidate any other reasons why
 * the executable file might be covered by the GNU General Public License.
 *
 * This exception applies only to the code released by the Ziher
 * Project under the name Ziher.  If you copy code from other
 * Ziher Project or Free Software Foundation releases into a copy of
 * Ziher, as the General Public License permits, the exception does
 * not apply to the code that you add in this way.  To avoid misleading
 * anyone as to the status of such modified files, you must delete
 * this exception notice from them.
 *
 * If you write modifications of your own for Ziher, it is your choice
 * whether to permit this exception to apply to your modifications.
 * If you do not wish that, delete this exception notice.
 *
 */

#include "file_io.zhh"


#define F_BLOCK  65536


THREAD STATIC t_hSrcFile
THREAD STATIC t_lSetDaTi := .T.
THREAD STATIC t_fileTime

/* This is a replacement for the CA-T*ols III function of the
   same name that causes GPF's. */
FUNCTION FileCopy( cSource, cDest, lMode )

   LOCAL hDstFile
   LOCAL cBuffer
   LOCAL lDone := .F.
   LOCAL nSrcBytes, nDstBytes, nTotBytes := 0

   IF t_hSrcFile != NIL
      zh_vfClose( t_hSrcFile )
   ENDIF
   IF ( t_hSrcFile := zh_vfOpen( cSource, FO_READ ) ) != NIL
      IF ( hDstFile := zh_vfOpen( cDest, FO_CREAT + FO_TRUNC + FO_WRITE ) ) != NIL
         zh_default( @lMode, .F. )
         cBuffer := Space( F_BLOCK )
         DO WHILE .T.
            IF ( nSrcBytes := zh_vfRead( t_hSrcFile, @cBuffer, F_BLOCK ) ) <= 0
               lDone := .T.
               EXIT
            ENDIF
            IF ( nDstBytes := zh_vfWrite( hDstFile, cBuffer, nSrcBytes ) ) > 0
               nTotBytes += nDstBytes
            ENDIF
            IF nDstBytes < nSrcBytes
               IF lMode
                  zh_vfSeek( t_hSrcFile, nDstBytes - nSrcBytes, FS_RELATIVE )
               ENDIF
               EXIT
            ENDIF
         ENDDO
         zh_vfClose( hDstFile )
         IF lDone .OR. ! lMode
            zh_vfClose( t_hSrcFile )
            t_hSrcFile := NIL
         ENDIF
         zh_vfTimeGet( cSource, @t_fileTime )
         IF t_lSetDaTi .and. ! Empty( t_fileTime )
            zh_vfTimeSet( cDest, t_fileTime )
         ENDIF
      ELSE
         zh_vfClose( t_hSrcFile )
         t_hSrcFile := NIL
      ENDIF
   ENDIF

   RETURN nTotBytes

FUNCTION FileCOpen()
   RETURN t_hSrcFile != NIL

FUNCTION FileCDaTi( lNewMode )

   LOCAL lOldMode := t_lSetDaTi

   IF ZH_ISLOGICAL( lNewMode )
      t_lSetDaTi := lNewMode
   ENDIF

   RETURN lOldMode

FUNCTION FileCCont( cDest )

   LOCAL hDstFile
   LOCAL cBuffer
   LOCAL lDone := .F.
   LOCAL nSrcBytes, nDstBytes, nTotBytes := 0

   IF t_hSrcFile != NIL
      IF ( hDstFile := zh_vfOpen( cDest, FO_CREAT + FO_TRUNC + FO_WRITE ) ) != NIL
         cBuffer := Space( F_BLOCK )
         DO WHILE .T.
            IF ( nSrcBytes := zh_vfRead( t_hSrcFile, @cBuffer, F_BLOCK ) ) <= 0
               lDone := .T.
               EXIT
            ENDIF
            IF ( nDstBytes := zh_vfWrite( hDstFile, cBuffer, nSrcBytes ) ) > 0
               nTotBytes += nDstBytes
            ENDIF
            IF nDstBytes < nSrcBytes
               EXIT
            ENDIF
         ENDDO
         zh_vfClose( hDstFile )
         IF lDone
            zh_vfClose( t_hSrcFile )
            t_hSrcFile := NIL
         ENDIF
         IF t_lSetDaTi .and. ! Empty( t_fileTime )
            zh_vfTimeSet( cDest, t_fileTime )
         ENDIF
      ENDIF
   ENDIF

   RETURN nTotBytes

FUNCTION FileCCLose()

   IF t_hSrcFile != NIL
      zh_vfClose( t_hSrcFile )
      t_hSrcFile := NIL
      RETURN .T.
   ENDIF

   RETURN .F.

FUNCTION FileAppend( cSrc, cDest )

   LOCAL cBuffer
   LOCAL hSrcFile, hDstFile
   LOCAL nSrcBytes, nDstBytes, nTotBytes := 0

   IF ( hSrcFile := zh_vfOpen( cSrc, FO_READ ) ) != NIL

      IF zh_vfExists( cDest )
         IF ( hDstFile := zh_vfOpen( cDest, FO_WRITE ) ) != NIL
            zh_vfSeek( hDstFile, 0, FS_END )
         ENDIF
      ELSE
         hDstFile := zh_vfOpen( cDest, FO_CREAT + FO_TRUNC + FO_WRITE )
      ENDIF

      IF hDstFile != NIL
         cBuffer := Space( F_BLOCK )
         DO WHILE .T.
            IF ( nSrcBytes := zh_vfRead( hSrcFile, @cBuffer, F_BLOCK ) ) <= 0
               EXIT
            ENDIF
            IF ( nDstBytes := zh_vfWrite( hDstFile, cBuffer, nSrcBytes ) ) < nSrcBytes
               EXIT
            ENDIF
            nTotBytes += nDstBytes
         ENDDO
         zh_vfClose( hDstFile )
      ENDIF
      zh_vfClose( hSrcFile )
   ENDIF

   RETURN nTotBytes
